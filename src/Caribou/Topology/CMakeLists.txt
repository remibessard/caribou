project(Topology)

set(HEADER_FILES
    config.h.in
    BarycentricContainer.h
    BaseMesh.h
    BaseDomain.h
    Domain.h
    Grid/Grid.h
    Grid/Internal/BaseGrid.h
    Grid/Internal/BaseMultidimensionalGrid.h
    Grid/Internal/BaseUnidimensionalGrid.h
    HashGrid.h
    Mesh.h
)

set(TARGET_TYPE "INTERFACE")
set(TARGET_VISIBILITY "INTERFACE")
if (CARIBOU_WITH_VTK)
    find_package(VTK COMPONENTS ${CARIBOU_VTK_MODULES} REQUIRED)
    set(TARGET_TYPE "SHARED")
    set(TARGET_VISIBILITY "PUBLIC")
    list(APPEND HEADER_FILES IO/VTKReader.h)
    list(APPEND SOURCE_FILES IO/VTKReader.cpp)
    if (VTK_VERSION VERSION_LESS "8.90.0")
        # old system
        include(${VTK_USE_FILE})
    endif()
endif()

find_package(Eigen3 QUIET REQUIRED)

add_library(${PROJECT_NAME} ${TARGET_TYPE} ${SOURCE_FILES})
add_library(Caribou::${PROJECT_NAME} ALIAS ${PROJECT_NAME})

target_link_libraries (${PROJECT_NAME} ${TARGET_VISIBILITY} Eigen3::Eigen)
target_link_libraries(${PROJECT_NAME} ${TARGET_VISIBILITY} Caribou::Geometry)
target_link_libraries(${PROJECT_NAME} ${TARGET_VISIBILITY}
                      $<$<AND:$<PLATFORM_ID:Linux>,$<VERSION_LESS:$<CXX_COMPILER_VERSION>,8.0>>:stdc++fs>
                      $<$<AND:$<PLATFORM_ID:Darwin>,$<VERSION_LESS:$<CXX_COMPILER_VERSION>,9.0>>:c++fs>)
target_compile_definitions(${PROJECT_NAME} ${TARGET_VISIBILITY}
                           $<$<AND:$<PLATFORM_ID:Linux>,$<VERSION_LESS:$<CXX_COMPILER_VERSION>,8.0>>:LEGACY_CXX>
                           $<$<AND:$<PLATFORM_ID:Darwin>,$<VERSION_LESS:$<CXX_COMPILER_VERSION>,9.0>>:LEGACY_CXX>)
if (CARIBOU_WITH_VTK)
    target_link_libraries(${PROJECT_NAME} PUBLIC ${VTK_LIBRARIES})
endif()

if (VTK_VERSION VERSION_GREATER_EQUAL "8.90.0")
    vtk_module_autoinit(
        TARGETS ${PROJECT_NAME}
        MODULES ${VTK_LIBRARIES}
    )
endif()

## Install rules for the library and headers; CMake package configurations files
sofa_create_component_in_package_with_targets(
    COMPONENT_VERSION ${CARIBOU_VERSION}
    TARGETS ${PROJECT_NAME} AUTO_SET_TARGET_PROPERTIES
    COMPONENT_NAME ${PROJECT_NAME}
    PACKAGE_NAME Caribou
    PUBLIC_HEADERS ${HEADER_FILES}
    INCLUDE_SOURCE_DIR "${CMAKE_CURRENT_SOURCE_DIR}/../.."
    INCLUDE_BUILD_DIR "${CMAKE_BINARY_DIR}/include/Caribou/Topology"
    INCLUDE_INSTALL_DIR "Caribou/Topology"
    RELOCATABLE "../../plugins"
)
